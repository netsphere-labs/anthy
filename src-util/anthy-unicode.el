;;; anthy-unicode.el -- Anthy

;; Copyright (C) 2001 - 2007 KMC(Kyoto University Micro Computer Club)
;; Copyright (C) 2021 Takao Fujiwara <takao.fujiwara1@gmail.com>
;; Copyright (c) 2021 Red Hat, Inc.

;; Author: Yusuke Tabata<yusuke@kmc.gr.jp>
;;         Tomoharu Ugawa
;;         Norio Suzuki <suzuki@sanpobu.net>
;; Keywords: japanese

;; This file is part of Anthy

;;; Commentary:
;;
;; ã‹ãªæ¼¢å­—å¤‰æ›ã‚¨ãƒ³ã‚¸ãƒ³ Anthy ã‚’ emacs ã‹ã‚‰ä½¿ã†ãŸã‚ã®ãƒ—ãƒ­ã‚°ãƒ©ãƒ 
;; Anthy ãƒ©ã‚¤ãƒ–ãƒ©ãƒªã‚’ä½¿ã†ãŸã‚ã®ã‚³ãƒãƒ³ãƒ‰ anthy-agent-unicode ã‚’èµ·å‹•ã—ã¦ã€
;; anthy-agent-unicode ã¨ãƒ‘ã‚¤ãƒ—ã§é€šä¿¡ã‚’ã™ã‚‹ã“ã¨ã«ã‚ˆã£ã¦å¤‰æ›ã®å‹•ä½œã‚’è¡Œã†
;;
;;
;; Funded by IPAæœªè¸ã‚½ãƒ•ãƒˆã‚¦ã‚§ã‚¢å‰µé€ äº‹æ¥­ 2001 10/10
;;
;; é–‹ç™ºã¯ emacs21.2 ä¸Šã§è¡Œã£ã¦ã„ã¦ minor-mode
;; ã‚‚ã—ãã¯ leim ã¨ã—ã¦ã‚‚ä½¿ç”¨ã§ãã‚‹
;; (set-input-method 'japanese-anthy)
;;
;; emacs19(mule),20,21,xemacs ã§å‹•ä½œã™ã‚‹
;;
;;
;; 2003-08-24 XEmacs ã®å€™è£œé¸æŠãƒ¢ãƒ¼ãƒ‰ãƒã‚°ã«å¯¾å¿œ (suzuki)
;;
;; 2001-11-16 EUC-JP -> ISO-2022-JP
;; 2021-10-26 ISO-2022-JP -> UTF-8
;;
;; TODO
;;  å€™è£œé¸æŠãƒ¢ãƒ¼ãƒ‰ã§å€™è£œã‚’ã„ã£ãã«æ¬¡ã®ãƒšãƒ¼ã‚¸ã«ã„ã‹ãªã„ã‚ˆã†ã«ã™ã‚‹ (2chã‚¹ãƒ¬78)
;;  minibufffer ã®æ‰±ã„
;;  isearch å¯¾å¿œ
;;
;; ç”¨èª
;;  commit æ–‡å­—åˆ—ã‚’ç¢ºå®šã™ã‚‹ã“ã¨
;;  preedit(ãƒ—ãƒªã‚¨ãƒ‡ã‚£ãƒƒãƒˆ) ç¢ºå®šå‰ã®æ–‡å­—åˆ—ã‚¢ãƒ³ãƒ€ãƒ¼ãƒ©ã‚¤ãƒ³ã‚„å¼·èª¿ã®å±æ€§ã‚‚å«ã‚€
;;  segment(æ–‡ç¯€) æ–‡æ³•çš„ãªæ–‡ç¯€ã§ã¯ãªãï¼ŒåŒã˜å±æ€§ã®æ–‡å­—åˆ—ã®ã‹ãŸã¾ã‚Š
;;

;;; Code:
;(setq debug-on-error t)

(defvar anthy-default-enable-enum-candidate-p t
  "ã“ã‚Œã‚’è¨­å®šã™ã‚‹ã¨æ¬¡å€™è£œã‚’æ•°å›æŠ¼ã—ãŸéš›ã«å€™è£œã®ä¸€è¦§ã‹ã‚‰é¸æŠã™ã‚‹ãƒ¢ãƒ¼ãƒ‰ã«ãªã‚Šã¾ã™ï¼")

(defvar anthy-personality ""
  "ãƒ‘ãƒ¼ã‚½ãƒŠãƒªãƒ†ã‚£")

(defvar anthy-preedit-begin-mark "|"
  "å¤‰æ›æ™‚ã®å…ˆé ­ã«ä»˜ãæ–‡å­—åˆ—")

(defvar anthy-preedit-delim-mark "|"
 "å¤‰æ›æ™‚ã®æ–‡ç¯€ã®åŒºåˆ‡ã‚Šã«ä½¿ã‚ã‚Œã‚‹æ–‡å­—åˆ—")

(defvar anthy-accept-timeout 50)
(if (string-match "^22\." emacs-version)
    (setq anthy-accept-timeout 1))

(defconst anthy-working-buffer " *anthy*")
(defvar anthy-agent-unicode-process nil
  "anthy-agent-unicodeã®ãƒ—ãƒ­ã‚»ã‚¹")
(defvar anthy-use-hankaku-kana t)
;;
(defvar anthy-agent-unicode-command-list '("anthy-agent-unicode")
  "anthy-agent-unicodeã®PATH å")

;; face
(defvar anthy-highlight-face nil)
(defvar anthy-underline-face nil)
(copy-face 'highlight 'anthy-highlight-face)
(set-face-underline 'anthy-highlight-face t)
(copy-face 'underline 'anthy-underline-face)

;;
(defvar anthy-xemacs
  (if (featurep 'xemacs)
      t nil))
(if anthy-xemacs
    (require 'overlay))
;;
(defvar anthy-mode-map nil
  "Anthyã®ASCIIãƒ¢ãƒ¼ãƒ‰ã®ã‚­ãƒ¼ãƒãƒƒãƒ—")
(or anthy-mode-map
    (let ((map (make-keymap))
	  (i 32))
      (define-key map (char-to-string 10) 'anthy-insert)
      (define-key map (char-to-string 17) 'anthy-insert)
      (while (< i 127)
	(define-key map (char-to-string i) 'anthy-insert)
	(setq i (+ 1 i)))
      (setq anthy-mode-map map)))
;;
(defvar anthy-preedit-keymap nil
  "Anthyã®preeditã®ã‚­ãƒ¼ãƒãƒƒãƒ—")
(or anthy-preedit-keymap
    (let ((map (make-keymap))
	  (i 0))
      ;; é€šå¸¸ã®æ–‡å­—ã«å¯¾ã—ã¦
      (while (< i 128)
	(define-key map (char-to-string i) 'anthy-insert)
	(setq i (+ 1 i)))
      ;; æ–‡ç¯€ã®ä¼¸ç¸®
      (define-key map [(shift left)] 'anthy-insert)
      (define-key map [(shift right)] 'anthy-insert)
      ;; æ–‡ç¯€ã®ç§»å‹•
      (define-key map [left] 'anthy-insert)
      (define-key map [right] 'anthy-insert)
      (define-key map [backspace] 'anthy-insert)
      (setq anthy-preedit-keymap map)))

;; anthy-agent-unicode ã«é€ã‚‹éš›ã«ã‚­ãƒ¼ã‚’ã‚¨ãƒ³ã‚³ãƒ¼ãƒ‰ã™ã‚‹ãŸã‚ã®ãƒ†ãƒ¼ãƒ–ãƒ«
(defvar anthy-keyencode-alist
  '((1 . "(ctrl A)") ;; \C-a
    (2 . "(left)") ;; \C-b
    (4 . "(ctrl D)") ;; \C-d
    (5 . "(ctrl E)") ;; \C-e
    (6 . "(right)") ;; \C-f
    (7 . "(esc)") ;; \C-g
    (8 . "(ctrl H)") ;; \C-h
    (9 . "(shift left)") ;; \C-i
    (10 . "(ctrl J)")
    (11 . "(ctrl K)")
    (13 . "(enter)") ;; \C-m
    (14 . "(space)") ;; \C-n
    (15 . "(shift right)") ;; \C-o
    (16 . "(up)") ;; \C-p
    (32 . "(space)")
    (40 . "(opar)") ;; '('
    (41 . "(cpar)") ;; ')'
    (127 . "(ctrl H)")
    ;; emacs map
    (S-right . "(shift right)")
    (S-left . "(shift left)")
    (right . "(right)")
    (left . "(left)")
    (up . "(up)")
    (backspace . "(ctrl H)")
    ;; xemacs
    ((shift right) . "(shift right)")
    ((shift left) . "(shift left)")
    ((right) . "(right)")
    ((left) . "(left)")
    ((up) . "(up)"))
  "ã‚­ãƒ¼ã®ã‚¤ãƒ™ãƒ³ãƒˆã‚’anthy-agent-unicodeã«é€ã‚‹ãŸã‚ã®å¯¾å¿œè¡¨")

;; ãƒ¢ãƒ¼ãƒ‰ãƒ©ã‚¤ãƒ³ã®æ–‡å­—åˆ—
(defvar anthy-mode-line-string-alist
  '(("hiragana" . " ã‚")
    ("katakana" . " ã‚¢")
    ("alphabet" . " A")
    ("walphabet" . " ï¼¡")
    ("hankaku_kana" . " (I1")
    )
  "ãƒ¢ãƒ¼ãƒ‰åã¨ãƒ¢ãƒ¼ãƒ‰ãƒ©ã‚¤ãƒ³ã®æ–‡å­—åˆ—ã®å¯¾å¿œè¡¨")

;; æœ€å¾Œã«å‰²ã‚Šå½“ã¦ãŸcontext id
(defvar anthy-last-context-id 1)

;; From skk-macs.el From viper-util.el.  Welcome!
;; Fixed emacs batch-byte-compile of anthy.el about the below warning:
;; "anthy.el:163:1:Warning: !! The file uses old-style backquotes !!"
;; https://stackoverflow.com/questions/8109665/how-fix-emacs-error-old-style-backquotes-detected
(defmacro anthy-deflocalvar (var default-value &optional documentation)
  `(progn
       (defvar ,var ,default-value
	 ,(format "%s\n\(buffer local\)" documentation))
       (make-variable-buffer-local ',var)
       ))

;; buffer local variables
(anthy-deflocalvar anthy-context-id nil "ã‚³ãƒ³ãƒ†ã‚­ã‚¹ãƒˆã®id")
; ãƒ¢ãƒ¼ãƒ‰ã®ç®¡ç†
(anthy-deflocalvar anthy-minor-mode nil)
(anthy-deflocalvar anthy-mode nil)
(anthy-deflocalvar anthy-leim-active-p nil)
(anthy-deflocalvar anthy-saved-mode nil)
; ãƒ—ãƒªã‚¨ãƒ‡ã‚£ãƒƒãƒˆ
(anthy-deflocalvar anthy-preedit "")
(anthy-deflocalvar anthy-preedit-start 0)
(anthy-deflocalvar anthy-preedit-overlays '())
(anthy-deflocalvar anthy-mode-line-string " A")
; å€™è£œåˆ—æŒ™
(anthy-deflocalvar anthy-enum-candidate-p nil)
(anthy-deflocalvar anthy-enum-rcandidate-p nil)
(anthy-deflocalvar anthy-candidate-minibuffer "")
(anthy-deflocalvar anthy-enum-candidate-list '()
		   "ä»Šåˆ—æŒ™ã—ã¦ã„ã‚‹å€™è£œã®æƒ…å ±((ç”»é¢å†…ã®index å€™è£œã®index . å€™è£œæ–‡å­—åˆ—) ..)")
(anthy-deflocalvar anthy-enable-enum-candidate-p 
  (cons anthy-default-enable-enum-candidate-p nil)
  "ã“ã®ãƒãƒƒãƒ•ã‚¡ã§å€™è£œã®åˆ—æŒ™ã‚’è¡Œã†ã‹ã©ã†ã‹")
(anthy-deflocalvar anthy-current-candidate-index 0)
(anthy-deflocalvar anthy-current-candidate-layout-begin-index 0)
(anthy-deflocalvar anthy-current-candidate-layout-end-index 0)
; å…¥åŠ›çŠ¶æ…‹
(anthy-deflocalvar anthy-current-rkmap "hiragana")
; undo
(anthy-deflocalvar anthy-buffer-undo-list-saved nil)

;;
(defvar anthy-wide-space "ã€€" "ã‚¹ãƒšãƒ¼ã‚¹ã‚’æŠ¼ã—ãŸæ™‚ã«å‡ºã¦æ¥ã‚‹æ–‡å­—")

;;; setup minor-mode
;; minor-mode-alist
(if (not
     (assq 'anthy-minor-mode minor-mode-alist))
    (setq minor-mode-alist
       (cons
	(cons 'anthy-minor-mode '(anthy-mode-line-string))
	minor-mode-alist)))
;; minor-mode-map-alist
(if (not
     (assq 'anthy-minor-mode minor-mode-map-alist))
    (setq minor-mode-map-alist
       (cons
	(cons 'anthy-minor-mode anthy-mode-map)
	minor-mode-map-alist)))

;;
(defun anthy-process-sentinel (proc stat)
  "ãƒ—ãƒ­ã‚»ã‚¹ã®çŠ¶æ…‹ãŒå¤‰åŒ–ã—ãŸã‚‰å‚ç…§ã‚’æ¶ˆã—ã¦ï¼Œæ¬¡ã«å†èµ·å‹•ã§ãã‚‹ã‚ˆã†ã«ã™ã‚‹"
  (message "%s" stat)
  (anthy-mode-off)
  (setq anthy-agent-unicode-process nil))

;;; status
(defun anthy-update-mode-line ()
  "ãƒ¢ãƒ¼ãƒ‰ãƒ©ã‚¤ãƒ³ã‚’æ›´æ–°ã™ã‚‹"
  (let ((a (assoc anthy-current-rkmap anthy-mode-line-string-alist)))
    (if a
	(progn
	 (setq anthy-mode-line-string (cdr a))
	 (setq current-input-method-title
	       (concat "<Anthy:" (cdr a) ">")))))
  (force-mode-line-update))

;;; preedit control
(defun anthy-erase-preedit ()
  "ãƒ—ãƒªã‚¨ãƒ‡ã‚£ãƒƒãƒˆã‚’å…¨éƒ¨æ¶ˆã™"
  (if (> (string-width anthy-preedit) 0)
      (let* ((str anthy-preedit)
	     (len (length str))
	     (start anthy-preedit-start))
	(delete-region start (+ start len))
	(goto-char start)))
  (setq anthy-preedit "")
  (mapc 'delete-overlay anthy-preedit-overlays)
  (setq anthy-preedit-overlays nil))

(defun anthy-select-face-by-attr (attr)
  "æ–‡ç¯€ã®å±æ€§ã«å¿œã˜ãŸfaceã‚’è¿”ã™"
  (if (memq 'RV attr)
      'anthy-highlight-face
    'anthy-underline-face))

(defun anthy-enable-preedit-keymap ()
  "ã‚­ãƒ¼ãƒãƒƒãƒ—ã‚’ãƒ—ãƒªã‚¨ãƒ‡ã‚£ãƒƒãƒˆã®å­˜åœ¨ã™ã‚‹æ™‚ã®ã‚‚ã®ã«åˆ‡æ›¿ãˆã‚‹"
;  (setq anthy-saved-buffer-undo-list buffer-undo-list)
;  (buffer-disable-undo)
  (setcdr
   (assq 'anthy-minor-mode minor-mode-map-alist)
   anthy-preedit-keymap))

(defun anthy-disable-preedit-keymap ()
  "ã‚­ãƒ¼ãƒãƒƒãƒ—ã‚’ãƒ—ãƒªã‚¨ãƒ‡ã‚£ãƒƒãƒˆã®å­˜åœ¨ã—ãªã„æ™‚ã®ã‚‚ã®ã«åˆ‡æ›¿ãˆã‚‹"
;  (buffer-enable-undo)
;  (setq buffer-undo-list anthy-saved-buffer-undo-list)
  (setcdr
   (assq 'anthy-minor-mode minor-mode-map-alist)
   anthy-mode-map)
  (anthy-update-mode-line))

(defun anthy-insert-preedit-segment (str attr)
  "ãƒ—ãƒªã‚¨ãƒ‡ã‚£ãƒƒãƒˆã‚’ä¸€æ–‡ç¯€æ–‡è¿½åŠ ã™ã‚‹"
  (let ((start (point))
	(end) (ol))
    (cond ((or (memq 'ENUM attr) (memq 'ENUMR attr))
	   (setq str (concat "<" str ">")))
	  ((memq 'RV attr) 
	   (setq str (concat "[" str "]"))))
    ; ãƒ—ãƒªã‚¨ãƒ‡ã‚£ãƒƒãƒˆã®æ–‡å­—åˆ—ã‚’è¿½åŠ ã™ã‚‹
    (insert-and-inherit str)
    (setq end (point))
    ;; overlayã«ã‚ˆã£ã¦å±æ€§ã‚’è¨­å®šã™ã‚‹
    (setq ol (make-overlay start end))
    (overlay-put ol 'face (anthy-select-face-by-attr attr))
    (setq anthy-preedit-overlays
	  (cons ol anthy-preedit-overlays))
    str))

(defvar anthy-select-candidate-keybind
  '((0 . "a")
    (1 . "s")
    (2 . "d")
    (3 . "f")
    (4 . "g")
    (5 . "h")
    (6 . "j")
    (7 . "k")
    (8 . "l")
    (9 . ";")))

;;;
;;; auto fill controll
;;; from egg.el

(defun anthy-do-auto-fill ()
  (if (and auto-fill-function (> (current-column) fill-column))
      (let ((ocolumn (current-column)))
	(funcall auto-fill-function)
	(while (and (< fill-column (current-column))
		    (< (current-column) ocolumn))
	  (setq ocolumn (current-column))
	  (funcall auto-fill-function)))))

;;
(defun anthy-check-context-id ()
  "ãƒãƒƒãƒ•ã‚¡ã«ã‚³ãƒ³ãƒ†ã‚­ã‚¹ãƒˆidãŒå‰²ã‚ŠæŒ¯ã‚‰ã‚Œã¦ã„ã‚‹ã‹ã‚’ãƒã‚§ãƒƒã‚¯ã™ã‚‹"
  (if (null anthy-context-id)
      (progn
	(setq anthy-context-id anthy-last-context-id)
	(setq anthy-last-context-id
	      (+ anthy-last-context-id 1)))))

(defun anthy-get-candidate (idx)
  "agentã‹ã‚‰å€™è£œã‚’ä¸€ã¤å–å¾—ã™ã‚‹"
  (anthy-send-recv-command 
   (concat " GET_CANDIDATE "
	   (number-to-string idx) "\n")))

;; å€™è£œãƒªã‚¹ãƒˆã‹ã‚‰ãƒŸãƒ‹ãƒãƒƒãƒ•ã‚¡ã«è¡¨ç¤ºã™ã‚‹æ–‡å­—åˆ—ã‚’æ§‹æˆã™ã‚‹
(defun anthy-make-candidate-minibuffer-string ()
  (let ((cand-list anthy-enum-candidate-list)
	(cur-elm)
	(str))
    (while cand-list
      (setq cur-elm (car cand-list))
      (let ((cand-str (cdr (cdr cur-elm)))
	    (cand-idx (car (cdr cur-elm)))
	    (sel-idx (car cur-elm)))
	(setq str (format (if (= anthy-current-candidate-index cand-idx)
			      "%s:[%s] "
			      "%s: %s  ")
			  (cdr (assoc sel-idx anthy-select-candidate-keybind))
			  cand-str)))
      (setq anthy-candidate-minibuffer
	    (concat str
		    anthy-candidate-minibuffer))
      (setq cand-list (cdr cand-list)))))

;; è¡¨ç¤ºã™ã‚‹å€™è£œã®ãƒªã‚¹ãƒˆã«ç”»é¢å†…ã§ã®ã‚¤ãƒ³ãƒ‡ãƒƒã‚¯ã‚¹ã‚’ä»˜ã‘ã‚‹
(defun anthy-add-candidate-index (lst)
  (let ((i 0)
	(res nil))
    (while lst
      (setq res
	    (cons
	     (cons i (car lst))
	     res))
      (setq i (1+ i))
      (setq lst (cdr lst)))
    res))


;; æ–‡å­—ã®å¹…ã‚’è¨ˆç®—ã—ã¦ã€è¡¨ç¤ºã™ã‚‹å€™è£œã®ãƒªã‚¹ãƒˆã‚’ä½œã‚‹
(defun anthy-make-candidate-index-list (base nr l2r)
  (let ((width (frame-width))
	(errorp nil)
	(i 0)
	(repl)
	(cand-idx)
	(lst))
    ;; loop
    (while (and
	    (if l2r
		(< (+ base i) nr)
	      (<= 0 (- base i)))
	    (> width 0)
	    (< i (length anthy-select-candidate-keybind))
	    (not errorp))
      (if l2r
	  (setq cand-idx (+ base i))
	(setq cand-idx (- base i)))
      (setq repl (anthy-get-candidate cand-idx))
      (if (listp repl)
	  ;; append candidate
	  (let ((cand-str (car repl)))
	    (setq width (- width (string-width cand-str) 5))
	    (if (or (> width 0) (null lst))
		(setq lst
		      (cons
		       (cons cand-idx cand-str)
		       lst))))
	;; erroneous candidate
	(setq errorp t))
      (setq i (1+ i)))
    (if errorp
	nil
      lst)))
  

;; è¡¨ç¤ºã™ã‚‹å€™è£œã®ãƒªã‚¹ãƒˆã‚’ä½œã‚‹
(defun anthy-calc-candidate-layout (base nr l2r)
  (let
      ((lst (anthy-make-candidate-index-list base nr l2r)))
    ;;ã‚«ãƒ¬ãƒ³ãƒˆã®å€™è£œç•ªå·ã‚’è¨­å®šã™ã‚‹
    (if l2r
	(progn
	  ;; å·¦ã‹ã‚‰å³ã®å ´åˆ
	  ;; indexã‚’ä¸€ç•ªå³ã®å€™è£œã«è¨­å®šã™ã‚‹
	  (anthy-get-candidate (car (car lst)))
	  (setq lst (reverse lst))
	  (setq anthy-current-candidate-index (car (car lst))))
      (progn
	;; å³ã‹ã‚‰å·¦ã®å ´åˆ
	(setq anthy-current-candidate-index (car (car (reverse lst))))))
    ;;çµæœã‚’ã‚»ãƒƒãƒˆ
    (setq anthy-enum-candidate-list
	  (if lst
	      (anthy-add-candidate-index lst)
	    nil))))

;;
(defun anthy-layout-candidate (idx nr)
  "å€™è£œãƒªã‚¹ãƒˆã‚’minibufferã¸ãƒ¬ã‚¤ã‚¢ã‚¦ãƒˆã™ã‚‹"
  (setq anthy-candidate-minibuffer "")
  (setq anthy-enum-candidate-list '())
  ;; å·¦->å³ or å³->å·¦ã«ãƒ¬ã‚¤ã‚¢ã‚¦ãƒˆã™ã‚‹
  (if anthy-enum-candidate-p
      (anthy-calc-candidate-layout idx nr 't)
    (anthy-calc-candidate-layout idx nr nil))
  (anthy-make-candidate-minibuffer-string)
  ;; çµæœã‚’è¡¨ç¤ºã™ã‚‹
  (if anthy-enum-candidate-list
      (progn
	(message "%s" anthy-candidate-minibuffer)
	(setq anthy-current-candidate-layout-begin-index
	      (car (cdr (car (reverse anthy-enum-candidate-list)))))
	(setq anthy-current-candidate-layout-end-index
	      (car (cdr (car anthy-enum-candidate-list)))))

    nil))

(defun anthy-update-preedit (stat ps)
  "ãƒ—ãƒªã‚¨ãƒ‡ã‚£ãƒƒãƒˆã‚’æ›´æ–°ã™ã‚‹"
  (let ((cursor-pos nil)
	(num-candidate 0)
	(idx-candidate 0)
	(enum-candidate
	 (or anthy-enum-candidate-p
	     anthy-enum-rcandidate-p)))
    ;; erase old preedit
    (anthy-erase-preedit)

    ;; å…¥åŠ›ã‚­ãƒ£ãƒ³ã‚»ãƒ«æ™‚ã«undoãƒªã‚¹ãƒˆã‚’ç¹‹ã’ã‚‹
    (if (and (= (length ps) 0)  anthy-buffer-undo-list-saved )
	(progn
;	  (message "enable")
	  (buffer-enable-undo)
	  (setq buffer-undo-list anthy-buffer-undo-list)
	  (setq anthy-buffer-undo-list-saved nil)
	  ))

    (anthy-disable-preedit-keymap)
    ;; insert new preedit
    (setq anthy-preedit-start (point))
    (setq anthy-enum-candidate-p nil)
    (setq anthy-enum-rcandidate-p nil)
    (if (member stat '(2 3 4))
	(progn
	  (setq anthy-preedit
		(concat anthy-preedit anthy-preedit-begin-mark))
	  (anthy-insert-preedit-segment anthy-preedit-begin-mark '())

	  ;; å…¥åŠ›é–‹å§‹ã¨åŒæ™‚ã«undoãƒªã‚¹ãƒˆã‚’ç„¡åŠ¹åŒ–
	  (if (not anthy-buffer-undo-list-saved)
	      (progn
		;(message "disable")
		(setq anthy-buffer-undo-list (cdr buffer-undo-list))
		(buffer-disable-undo)
		(setq anthy-buffer-undo-list-saved 't)
		)
	    ;(message "not saved")
	    )

	  ))

    ;; å„æ–‡ç¯€ã«å¯¾ã—ã¦
    (while ps
      (let ((cur (car ps)))
	(setq ps (cdr ps))
	(cond
	 ((eq cur 'cursor)
	  (setq cursor-pos (point)))
	 ((string-equal (car (cdr cur)) "")
	  nil)
	 (t
	  (let ((nr (car (cdr (cdr (cdr cur)))))
		(idx (car (cdr (cdr cur))))
		(str (car (cdr cur)))
		(attr (car cur)))
	    (setq str (anthy-insert-preedit-segment str attr))
	    (cond ((and (car anthy-enable-enum-candidate-p) (memq 'ENUM attr))
		   ;; é †æ–¹å‘ã®å€™è£œåˆ—æŒ™
		   (setq anthy-enum-candidate-p t)
		   (setq idx-candidate idx)
		   (setq num-candidate nr))
		  ((and (car anthy-enable-enum-candidate-p) (memq 'ENUMR attr))
		   ;; é€†æ–¹å‘ã®å€™è£œåˆ—æŒ™
		   (setq anthy-enum-rcandidate-p t)
		   (setq idx-candidate idx)
		   (setq num-candidate nr)))
	    (setq anthy-preedit
		  (concat anthy-preedit str))
	    (if (and (member stat '(3 4)) (not (eq ps '())))
		(progn
		  (setq anthy-preedit
			(concat anthy-preedit anthy-preedit-delim-mark))
		  (anthy-insert-preedit-segment anthy-preedit-delim-mark '()))))))))
    ;; å€™è£œä¸€è¦§ã®è¡¨ç¤ºé–‹å§‹ãƒã‚§ãƒƒã‚¯
    (if (and (not enum-candidate)
	     (or anthy-enum-candidate-p anthy-enum-rcandidate-p))
	(setq anthy-current-candidate-layout-begin-index 0))
    ;; å€™è£œã®åˆ—æŒ™ã‚’è¡Œã†
    (if (or anthy-enum-candidate-p anthy-enum-rcandidate-p)
	(anthy-layout-candidate idx-candidate num-candidate))
    ;; preeditã®keymapã‚’æ›´æ–°ã™ã‚‹
    (if (member stat '(2 3 4))
	(anthy-enable-preedit-keymap))
    (if cursor-pos (goto-char cursor-pos))))

; suzuki : Emacs / XEmacs ã§å…±é€šã®é–¢æ•°å®šç¾©
(defun anthy-encode-key (ch)
  (let ((c (assoc ch anthy-keyencode-alist)))
    (if c
	(cdr c)
      (if (and
	   (integerp ch)
	   (> ch 32))
	  (char-to-string ch)
	nil))))

(defun anthy-restore-undo-list (commit-str)
  (let* ((len (length commit-str))
	 (beginning (point))
	 (end (+ beginning len)))
    (setq buffer-undo-list
	  (cons (cons beginning end)
		(cons nil anthy-saved-buffer-undo-list)))
	 ))

(defun anthy-proc-agent-reply (repl)
  (let*
      ((stat (car repl))
       (body (cdr repl))
       (commit "")
       (commitlen nil)
       (preedit nil))
    ;; å„æ–‡ç¯€ã‚’å‡¦ç†ã™ã‚‹
    (while body
      (let* ((cur (car body))
	     (pe nil))
	(setq body (cdr body))
	(if (and
	     (listp cur)
	     (listp (car cur)))
	    (cond
	     ((eq (car (car cur)) 'COMMIT)
	      (setq commit (concat commit (car (cdr cur)))))
	     ((eq (car (car cur)) 'CUTBUF)
	      (let ((len (length (car (cdr cur)))))
		(copy-region-as-kill (point) (+ (point) len))))
	     ((memq 'UL (car cur))
	      (setq pe (list cur))))
	  (setq pe (list cur)))
	(if pe
	    (setq preedit (append preedit pe)))))
    ;; ã‚³ãƒŸãƒƒãƒˆã•ã‚ŒãŸæ–‡ç¯€ã‚’å‡¦ç†ã™ã‚‹
;    (anthy-restore-undo-list commit)
    (if (> (string-width commit) 0)
	(progn
	  (setq commitlen (length commit))
	  (anthy-erase-preedit)
	  (anthy-disable-preedit-keymap)
	  ; å…ˆã«ã‚³ãƒŸãƒƒãƒˆã•ã›ã¦ãŠã
	  (insert-and-inherit commit)
	  (anthy-do-auto-fill)

	  ;; ã‚³ãƒŸãƒƒãƒˆæ™‚ã«ç¹‹ã’ã‚‹
	  (if anthy-buffer-undo-list-saved 
	      (progn
		;(message "enable")
		; å¾©å¸°ã•ã›ã‚‹å‰ã«ï¼Œä»Šcommitã—ãŸå†…å®¹ã‚’ãƒªã‚¹ãƒˆã«è¿½åŠ 
		(setq anthy-buffer-undo-list
		      (cons (cons anthy-preedit-start
				  (+ anthy-preedit-start commitlen))
			    anthy-buffer-undo-list))
		(setq anthy-buffer-undo-list (cons nil anthy-buffer-undo-list))

		(buffer-enable-undo)

		(setq buffer-undo-list anthy-buffer-undo-list)

		(setq anthy-buffer-undo-list-saved nil)
		))

	  (run-hooks 'anthy-commit-hook)
	  ))
    (anthy-update-preedit stat preedit)
    (anthy-update-mode-line)))

(defun anthy-insert-select-candidate (ch)
  (let* ((key-idx (car (rassoc (char-to-string ch)
			       anthy-select-candidate-keybind)))
	 (idx (car (cdr (assq key-idx
			      anthy-enum-candidate-list)))))
    (if idx
	(progn
	  (let ((repl (anthy-send-recv-command
		       (format " SELECT_CANDIDATE %d\n" idx))))
	    (anthy-proc-agent-reply repl))
	  (setq anthy-enum-candidate-p nil)
	  (setq anthy-enum-rcandidate-p nil))
      (message "%s" anthy-candidate-minibuffer))))

(defvar anthy-default-rkmap-keybind
  '(
    ;; q
    (("hiragana" . 113) . "katakana")
    (("katakana" . 113) . "hiragana")
    ;; l
    (("hiragana" . 108) . "alphabet")
    (("katakana" . 108) . "alphabet")
    ;; L
    (("hiragana" . 76) . "walphabet")
    (("katakana" . 76) . "walphabet")
    ;; \C-j
    (("alphabet" . 10) . "hiragana")
    (("walphabet" . 10) . "hiragana")
    ;; \C-q
    (("hiragana" . 17) . "hankaku_kana")
    (("hankaku_kana" . 17) . "hiragana")
    ))


(defvar anthy-rkmap-keybind anthy-default-rkmap-keybind)


(defun anthy-find-rkmap-keybind (ch)
  (let ((res
	 (assoc (cons anthy-current-rkmap ch) anthy-rkmap-keybind)))
    (if (and res (string-equal (cdr res) "hankaku_kana"))
	(if anthy-use-hankaku-kana res nil)
      res)))

(defun anthy-handle-normal-key (chenc)
  (let* ((repl
	  (if chenc (anthy-send-recv-command 
		     (concat chenc "\n"))
	    nil)))
    (if repl
	(anthy-proc-agent-reply repl))))

(defun anthy-handle-enum-candidate-mode (chenc)
  (anthy-handle-normal-key chenc))

;;
(defun anthy-insert (&optional arg)
  "Anthyã®ã‚­ãƒ¼ãƒãƒ³ãƒ‰ãƒ©"
  (interactive "*p")
  ;; suzuki : last-command-char ã‚’ (anthy-last-command-char) ã«å¤‰æ›´
  (let* ((ch (anthy-last-command-char))
	 (chenc (anthy-encode-key ch)))
    (anthy-handle-key ch chenc)))

(defun anthy-handle-key (ch chenc)
  (cond
   ;; å€™è£œé¸æŠãƒ¢ãƒ¼ãƒ‰ã‹ã‚‰å€™è£œã‚’é¸ã¶
   ((and (or anthy-enum-candidate-p anthy-enum-rcandidate-p)
	 (integerp ch)
	 (assq (car (rassoc (char-to-string ch)
			    anthy-select-candidate-keybind))
	       anthy-enum-candidate-list))
    (anthy-insert-select-candidate ch))
   ;; ã‚­ãƒ¼ãƒãƒƒãƒ—ã‚’å¤‰æ›´ã™ã‚‹ã‚³ãƒãƒ³ãƒ‰ã‚’å‡¦ç†ã™ã‚‹
   ((and (anthy-find-rkmap-keybind ch)
	 (string-equal anthy-preedit ""))
    (let ((mapname (cdr (anthy-find-rkmap-keybind ch))))
      (let ((repl (anthy-send-recv-command
		   (concat " MAP_SELECT " mapname "\n"))))
	(if (eq repl 'OK)
	    (progn
	      (setq anthy-current-rkmap
		    (cdr (assoc (cons anthy-current-rkmap ch)
				anthy-rkmap-keybind)))
	      (anthy-update-mode-line))))))
   ;; ã‚¢ãƒ«ãƒ•ã‚¡ãƒ™ãƒƒãƒˆãƒ¢ãƒ¼ãƒ‰ã®å ´åˆã¯ç›´æ¥å…¥åŠ›
   ((and (string-equal anthy-current-rkmap "alphabet")
	 (string-equal anthy-preedit ""))
    (self-insert-command 1))
   ;; ãƒ—ãƒªã‚¨ãƒ‡ã‚£ãƒƒãƒˆãŒãªãã¦ã‚¹ãƒšãƒ¼ã‚¹ãŒæŠ¼ã•ã‚ŒãŸ
   ((and
     (string-equal anthy-preedit "")
     (= ch 32)
     (not
      (string-equal anthy-current-rkmap "alphabet")))
    (progn
      (insert-and-inherit anthy-wide-space)
      (anthy-do-auto-fill)))
   ((or anthy-enum-candidate-p anthy-enum-rcandidate-p)
    (anthy-handle-enum-candidate-mode chenc))
   ;; æ™®é€šã®å…¥åŠ›
   (t
    (anthy-handle-normal-key chenc))))

;;
(defun anthy-do-invoke-agent (cmd)
  (if (and (stringp anthy-personality)
	   (> (length anthy-personality) 0))
      (start-process "anthy-agent-unicode"
		     anthy-working-buffer
		     cmd
		     (concat " --personality=" anthy-personality))
    (start-process "anthy-agent-unicode"
		   anthy-working-buffer
		   cmd)))
;;
(defun anthy-invoke-agent ()
  (let ((list anthy-agent-unicode-command-list)
	(proc nil))
    (while (and list (not proc))
      (setq proc 
	    (anthy-do-invoke-agent (car list)))
      (if (not (boundp 'proc))
	  (setq proc nil))
      (setq list (cdr list)))
    proc))
;;
;;
;;
(defun anthy-check-agent ()
  ;; check and do invoke
  (if (not anthy-agent-unicode-process)
      (let
	  ((proc (anthy-invoke-agent)))
	(if anthy-agent-unicode-process
	    (kill-process anthy-agent-unicode-process))
	(setq anthy-agent-unicode-process proc)
	(set-process-query-on-exit-flag proc nil)
;;	(if anthy-xemacs
;;	    (if (coding-system-p (find-coding-system 'euc-japan))
;;		(set-process-coding-system proc 'euc-japan 'euc-japan))
;;	  (cond ((coding-system-p 'euc-japan)
;;		 (set-process-coding-system proc 'euc-japan 'euc-japan))
;;		((coding-system-p '*euc-japan*)
;;		 (set-process-coding-system proc '*euc-japan* '*euc-japan*))))
	(set-process-sentinel proc 'anthy-process-sentinel))))
;;
(defun anthy-do-send-recv-command (cmd)
  (if (not anthy-agent-unicode-process)
      (anthy-check-agent))
  (let ((old-buffer (current-buffer)))
    (unwind-protect
	(progn
	  (set-buffer anthy-working-buffer)
	  (erase-buffer)
	  (process-send-string anthy-agent-unicode-process cmd)
	  (while (= (buffer-size) 0)
	    (accept-process-output nil 0 anthy-accept-timeout))
	  (read (buffer-string)))
      (set-buffer old-buffer))))
;;
(defun anthy-send-recv-command (cmd)
  (if anthy-context-id
      (anthy-do-send-recv-command
       (concat " SELECT_CONTEXT "
	       (number-to-string anthy-context-id)
	       "\n")))
  (anthy-do-send-recv-command cmd))
;;
(defun anthy-minibuffer-enter ()
  (setq anthy-saved-mode anthy-mode)
  (setq anthy-mode nil)
  (setq anthy-enable-enum-candidate-p 
	(cons nil anthy-enable-enum-candidate-p))
  (anthy-update-mode))
;;
(defun anthy-minibuffer-exit ()
  (setq anthy-mode anthy-saved-mode)
  (setq anthy-enable-enum-candidate-p 
	(cdr anthy-enable-enum-candidate-p))
  (anthy-update-mode))
;;
(defun anthy-kill-buffer ()
  (if anthy-context-id
      (anthy-send-recv-command
       " RELEASE_CONTEXT\n")))
;;
(defun anthy-mode-on ()
  (add-hook 'minibuffer-setup-hook 'anthy-minibuffer-enter)
  (add-hook 'minibuffer-exit-hook 'anthy-minibuffer-exit)
  (add-hook 'kill-buffer-hook 'anthy-kill-buffer)
  (anthy-check-context-id)
  (setq anthy-minor-mode t)
  (anthy-update-mode-line))
;;
(defun anthy-mode-off ()
  (setq anthy-minor-mode nil)
  (anthy-update-mode-line))
;;
(defun anthy-update-mode ()
  (if (or anthy-mode anthy-leim-active-p)
      (progn
	(anthy-check-agent)
	(anthy-mode-on))
    (anthy-mode-off))
  (run-hooks 'anthy-mode-hook))

(defun anthy-mode (&optional arg)
  "Start Anthy conversion system."
  (interactive "P")
  (setq anthy-mode
        (if (null arg)
            (not anthy-mode)
          (> (prefix-numeric-value arg) 0)))
  (anthy-update-mode))

(defun anthy-select-map (map)
  (anthy-send-recv-command (concat " MAP_SELECT " map "\n"))
  (setq anthy-current-rkmap map)
  (anthy-update-mode-line))
;;
(defun anthy-hiragana-map (&optional arg)
  "Hiragana mode"
  (interactive "P")
  (anthy-select-map "hiragana"))
;;
(defun anthy-katakana-map (&optional arg)
  "Katakana mode"
  (interactive "P")
  (anthy-select-map "katakana"))
;;
(defun anthy-alpha-map (arg)
  "Alphabet mode"
  (interactive "P")
  (anthy-select-map "alphabet"))
;;
(defun anthy-wide-alpha-map (arg)
  "Wide Alphabet mode"
  (interactive "P")
  (anthy-select-map "walphabet"))
;;
(defun anthy-hankaku-kana-map (arg)
  "Hankaku Katakana mode"
  (interactive "P")
  (anthy-select-map "hankaku_kana"))
;;
;;
;; leim ã® inactivate
;;
(defun anthy-unicode-leim-inactivate ()
  (setq anthy-leim-active-p nil)
  (anthy-update-mode))
;;
;; leim ã® activate
;;
(defun anthy-unicode-leim-activate (&optional name)
  (setq deactivate-current-input-method-function 'anthy-unicode-leim-inactivate)
  (setq anthy-leim-active-p t)
  (anthy-update-mode)
  (when (eq (selected-window) (minibuffer-window))
    (add-hook 'minibuffer-exit-hook 'anthy-unicode-leim-exit-from-minibuffer)))

;;
;; emacsã®ãƒã‚°é¿ã‘ã‚‰ã—ã„ã§ã™
;;
(defun anthy-unicode-leim-exit-from-minibuffer ()
  (deactivate-input-method)
  (when (<= (minibuffer-depth) 1)
    (remove-hook 'minibuffer-exit-hook 'anthy-unicode-leim-exit-from-minibuffer)))

;;
;; Emacs / XEmacs ã‚³ãƒ³ãƒ‘ãƒãƒ–ãƒ«ãª last-command-char
;; suzuki : æ–°è¨­
;;
(defun anthy-last-command-char ()
  "æœ€å¾Œã®å…¥åŠ›ã‚¤ãƒ™ãƒ³ãƒˆã‚’è¿”ã™ã€‚XEmacs ã§ã¯ int ã«å¤‰æ›ã™ã‚‹"
  (if anthy-xemacs
      (let ((event last-command-event))
	(cond
	 ((event-matches-key-specifier-p event 'left)      2)
	 ((event-matches-key-specifier-p event 'right)     6)
	 ((event-matches-key-specifier-p event 'backspace) 8)
	 (t
	  (char-to-int (event-to-character event)))))
    last-command-event))

;;
;;
;;
;(global-set-key [(meta escape)] 'anthy-mode)
(provide 'anthy-unicode)

(require 'anthy-unicode-dic)
(require 'anthy-unicode-conf)

;; is it ok for i18n?
(set-language-info "Japanese" 'input-method "japanese-anthy")
(if (equal current-language-environment "Japanese")
    (progn
      (if (boundp 'default-input-method)
	  (setq-default default-input-method "japanese-anthy"))
      (setq default-input-method "japanese-anthy")))

(defun anthy-default-mode ()
  (interactive)
  (setq anthy-rkmap-keybind anthy-default-rkmap-keybind)
  (anthy-send-recv-command " MAP_CLEAR 1\n")
  (anthy-send-recv-command " SET_PREEDIT_MODE 0\n")
  (anthy-hiragana-map))

(defun anthy-insert-and-convert (ch)
  (interactive "P")
  (if (< 0 (length anthy-preedit))
      (progn
        (anthy-insert ch)
        (anthy-handle-normal-key "(space)"))
    (anthy-insert ch)))

;;;
;;; anthy.el ends here
